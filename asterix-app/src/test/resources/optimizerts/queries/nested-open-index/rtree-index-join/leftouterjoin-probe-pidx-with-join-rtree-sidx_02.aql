/*
 * Description  : Test that left-outer-join may use two available indexes, one for primary index in prob subtree and another for secondary rtree index in index subtree.
 * Issue        : 730, 741
 * Expected Res : Success
 * Date         : 8th May 2014
 */

drop dataverse test if exists;
create dataverse test;
use dataverse test;

create type TwitterUserType as closed {
	screen-name: string,
	lang: string,
	friends-count: int32,
	statuses-count: int32,
	name: string,
	followers-count: int32
}

create type TweetMessageNestedType as open {
	tweetid: int64,
        user: TwitterUserType,
	send-time: datetime,
        referred-topics: {{ string }},
	message-text: string,
	countA: int32,
	countB: int32
}

create type TweetMessageType as open {
	nested: TweetMessageNestedType
}

create dataset TweetMessages(TweetMessageType)
primary key nested.tweetid;

create index twmSndLocIx on TweetMessages(nested.sender-location: point) type rtree enforced;
create index msgCountAIx on TweetMessages(nested.countA) type btree;
create index msgCountBIx on TweetMessages(nested.countB) type btree;
create index msgTextIx on TweetMessages(nested.message-text) type keyword;

write output to nc1:"rttest/rtree-index-join_leftouterjoin-probe-pidx-with-join-rtree-sidx_02.adm";

for $t1 in dataset('TweetMessages')
let $n :=  create-circle($t1.nested.sender-location, 0.5)
where $t1.nested.tweetid < int64("10")
order by $t1.nested.tweetid
return {
"tweetid1": $t1.nested.tweetid,
"loc1":$t1.nested.sender-location,
"nearby-message": for $t2 in dataset('TweetMessages')
                             where spatial-intersect($t2.nested.sender-location, $n) and $t1.nested.tweetid != $t2.nested.tweetid
                             order by $t2.nested.tweetid
                             return {"tweetid2":$t2.nested.tweetid, "loc2":$t2.nested.sender-location}
};